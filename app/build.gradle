/*
 *  _____ _                       _             _     _     _
 * /  ___| |                     (_)           | |   (_)   | |
 * \ `--.| |__   ___  _ __  _ __  _ _ __   __ _| |    _ ___| |_
 *  `--. \ '_ \ / _ \| '_ \| '_ \| | '_ \ / _` | |   | / __| __|
 * /\__/ / | | | (_) | |_) | |_) | | | | | (_| | |___| \__ \ |_
 * \____/|_| |_|\___/| .__/| .__/|_|_| |_|\__, \_____/_|___/\__|
 *                   | |   | |             __/ |
 *                   |_|   |_|            |___/
 *
 * Copyright (C) 2021-2022 Sebastian GÃ¶ls
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 2 of the License only
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, write to the Free Software Foundation, Inc.,
 * 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
 */

plugins {
	id "com.android.application"
	id "kotlin-android"
	id "kotlin-kapt"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
	compileSdkVersion 33
	buildToolsVersion "33.0.0"

	defaultConfig {
		applicationId "pl.edu.pjwstk.s999844.shoppinglist"
		minSdkVersion 25
		targetSdkVersion 33
		versionCode 15
		versionName "v1.13.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments["room.incremental"] = "true"
			}
		}
	}
	signingConfigs {
		release {
			if (keystorePropertiesFile.exists()) {
				def keystoreProperties = new Properties()
				keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

				storeFile file(keystoreProperties.getProperty("storeFile"))
				storePassword keystoreProperties.getProperty("storePassword")
				keyAlias keystoreProperties.getProperty("keyAlias")
				keyPassword keystoreProperties.getProperty("keyPassword")
				v2SigningEnabled true
			}

		}
	}
	applicationVariants.all {
		variant -> variant.resValue "string", "versionName", variant.versionName
	}
	buildTypes {
		release {
			minifyEnabled true
			signingConfig keystorePropertiesFile.exists() ? signingConfigs.release : null
			proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = "1.8"
	}
	buildFeatures {
		viewBinding true
	}
	lint {
		abortOnError true
		warningsAsErrors true
		checkAllWarnings true
		showAll true
		// sometimes we use the same string for similuar but non-equal purposes
		disable "DuplicateStrings"
		// This is an open source project and we don't have a person of each language sitting around translating stuff for us
		disable "MissingTranslation"
		// the launcher icons are the only ones affected by this and they have transparency in them - no point in converting
		disable "ConvertToWebp"
		// easier to read if in xml and not in a bunch of lambdas within the setup method for the activity
		disable "UsingOnClickInXml"
		// two library updates in parallel will make both CI builds fail and cause dependabot to not merge either, requiring manual work
		disable "GradleDependency"
	}
}

dependencies {
	implementation "androidx.lifecycle:lifecycle-process:2.5.0"

	def androidx_navigation_version = "2.5.0"
	implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
	implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"

	def room_version = "2.4.2"
	implementation "androidx.room:room-runtime:$room_version"
	kapt "androidx.room:room-compiler:$room_version"
	implementation fileTree(include: ["*.jar"], dir: "libs")

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "androidx.core:core-ktx:1.8.0"
	implementation "androidx.appcompat:appcompat:1.4.2"
	implementation "com.google.android.material:material:1.6.1"
	implementation "androidx.constraintlayout:constraintlayout:2.1.4"

	testImplementation "junit:junit:4.13.2"
	androidTestImplementation "androidx.test.ext:junit:1.1.3"
	androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}

